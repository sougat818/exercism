apply plugin: 'com.github.spotbugs'

allprojects { project ->
    project.afterEvaluate {
        if (project.plugins.hasPlugin('java')) {

            project.apply plugin: 'com.github.spotbugs'

            project.configurations {
                spotbugsPlugins
            }

            project.dependencies {
                spotbugsPlugins "com.h3xstream.findsecbugs:findsecbugs-plugin:${findSecBugsPluginVersion}"
            }

            def classLoader = plugins['com.github.spotbugs'].class.classLoader
            def SpotBugsEffort = classLoader.findLoadedClass( 'com.github.spotbugs.snom.Effort' )
            def SpotBugsConfidence = classLoader.findLoadedClass( 'com.github.spotbugs.snom.Confidence' )
            project.spotbugs {
                toolVersion = "${spotbugsToolVersion}"
                showProgress = false
                ignoreFailures = false
                effort = SpotBugsEffort.MAX
                reportLevel = SpotBugsConfidence.LOW

            }

            project.spotbugsTest {
                enabled = false
            }
        }
    }
}

